# Note: This file should be cross-platform compatible

# Global variables
export STS_ENV_HOME="${STS_ENV_HOME:-$HOME/env}"
export STS_DOCS_HOME="${STS_DOCS_HOME:-$STS_ENV_HOME/docs}"
export STS_NOTES_HOME="${STS_NOTES_HOME:-$HOME/notes}"
export STS_PLANNER_HOME="${STS_PLANNER_HOME:-$HOME/planner}"

export STS_PROJECTS_HOME="${STS_PROJECTS_HOME:-$HOME/projects}"

export STS_OPT_HOME="${STS_OPT_HOME:-$HOME/opt}"
export STS_LOCAL_HOME="${STS_LOCAL_HOME:-$HOME/.local}"

# Usage: reset_fpath
#   This will reset the fpath by removing all directories that start with
#   $STS_ENV_HOME/libraries
reset_fpath() {
  local dir="$STS_ENV_HOME/libraries"
  local new_fpath=()

  local fpathitem
  for fpathitem in $fpath; do
    [[ "${fpathitem#$dir}" == "$fpathitem" && "$fpathitem" != "$STS_ENV_HOME/configs/zsh/lib" ]] && new_fpath+=("$fpathitem")
  done

  fpath=("${new_fpath[@]}")
}

# Usage: load_library LIBRARY_NAME
#   This will autoload all functions in the folder
#   $STS_ENV_LIBRARIES/$LIBRARY_PATH, unless $STS_LIB_DISABLE_[LIBRARY_PATH]
#   has been set.
load_library() {

  # Check to see if the library directory exists
  local lib_path="$1"
  if [[ ! -d "$lib_path" ]]; then
    echo "$lib_path does not exist"
    return 1
  fi

  # Check if the library has been disabled from loading
  local override_var_name="STS_LIB_DISABLE_${lib_path:t:r}"
  local override_var_value=$(eval echo \$$override_var_name)
  if [[ -n $override_var_value ]]; then
    echo "Skipped autoloading functions from $lib_path. $override_var_name is set"
    return 1
  fi

  # So globbing doesn't throw errors for empty directories, which stops
  # zsh from running
  setopt localoptions nullglob

  # Update fpath if the libraries have not been loaded
  fpath=("$lib_path" $fpath)

  # For each zsh script in the provided directory...
  local file
  for file in "$lib_path"/*; do

    # Unload the library if it has already been loaded
    unfunction "${file:t:r}" 2>/dev/null

    # Autoload the function
    autoload -Uz "${file:t:r}"
  done
}

# Load all libraries. Order doesn't matter here.
load_all_libraries() {
  local STS_ENV_LIBRARIES="$STS_ENV_HOME/libraries"

  # Reset the fpath
  reset_fpath

  # Load all libraries
  local dir
  for dir in "$STS_ENV_LIBRARIES"/*; do
    load_library "$dir"
  done

  load_library "$STS_ENV_HOME/configs/zsh/lib"
}

# load all libraries
load_all_libraries

load_setting base

# load application specific environments
load_app_env_configs
